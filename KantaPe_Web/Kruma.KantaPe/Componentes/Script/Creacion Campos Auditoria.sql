--AGREGAR CAMPOS DE AUDITORIA
-------------------------------

DECLARE
@TABLE_SCHEMA VARCHAR(100) = 'JRG_AdmInf',

--VARIABLES
@TABLE_NAME VARCHAR(100),
@COLUMN_NAME VARCHAR(100),
@COLUMN_TYPE VARCHAR(100),
@SQL NVARCHAR(400),
@INDICE INT = 1,
@TOTAL INT

--COLUMNAS A INSERTAR
DECLARE @COLUMNS AS TABLE(
ID INT,
NAME VARCHAR(100),
[TYPE] VARCHAR(100)
)

INSERT INTO @COLUMNS(ID,NAME,[TYPE]) VALUES (1,'UsuarioCreacion','varchar(20)')
INSERT INTO @COLUMNS(ID,NAME,[TYPE]) VALUES (2,'FechaCreacion','datetime')
INSERT INTO @COLUMNS(ID,NAME,[TYPE]) VALUES (3,'UsuarioModificacion','varchar(20)')
INSERT INTO @COLUMNS(ID,NAME,[TYPE]) VALUES (4,'FechaModificacion','datetime')

SELECT @TOTAL = COUNT(1) FROM @COLUMNS
 
--TABLAS A ACTUALIZAR
DECLARE DB_CURSOR CURSOR FOR  
SELECT TABLE_NAME 
FROM INFORMATION_SCHEMA.TABLES 
WHERE
TABLE_TYPE = 'BASE TABLE' AND
TABLE_SCHEMA = @TABLE_SCHEMA
--AND TABLE_NAME like '%TablaTemp%' 

OPEN DB_CURSOR   
FETCH NEXT FROM DB_CURSOR INTO @TABLE_NAME   

WHILE @@FETCH_STATUS = 0   
BEGIN   
	SET @INDICE = 1
		
	WHILE (@INDICE <= @TOTAL)
	BEGIN
		SELECT @COLUMN_NAME = NAME, @COLUMN_TYPE = [TYPE]
		FROM @COLUMNS
		WHERE ID = @INDICE

		IF EXISTS(
		SELECT COLUMN_NAME
		FROM INFORMATION_SCHEMA.COLUMNS
		WHERE 
		TABLE_SCHEMA = @TABLE_SCHEMA AND
		TABLE_NAME = @TABLE_NAME AND
		COLUMN_NAME = @COLUMN_NAME
		)
		BEGIN
		SET @SQL  = 'ALTER TABLE ['+ @TABLE_SCHEMA +'].['+ @TABLE_NAME +'] DROP COLUMN ' + @COLUMN_NAME
		EXECUTE sp_executesql @SQL
		END

		SET @SQL  = 'ALTER TABLE ['+ @TABLE_SCHEMA +'].['+ @TABLE_NAME +'] ADD ' + @COLUMN_NAME + ' ' + @COLUMN_TYPE
		EXECUTE sp_executesql @SQL

		SET @INDICE = @INDICE + 1
	END

	FETCH NEXT FROM DB_CURSOR INTO @TABLE_NAME   
END   

CLOSE DB_CURSOR   
DEALLOCATE DB_CURSOR